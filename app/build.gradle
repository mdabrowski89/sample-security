apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion Versions.compiledSdk
    buildToolsVersion Versions.buildTools

    defaultConfig {
        project.ext.appName = 'sample-security'
        applicationId "pl.mobite.sample.security"
        minSdkVersion Versions.minSdk
        targetSdkVersion Versions.targetSdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            applicationIdSuffix ".debug"
            versionNameSuffix "-dev"
            resValue "string", "app_name", "$project.ext.appName-dev"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", project.ext.appName
        }
    }

    dexOptions {
        javaMaxHeapSize "3g"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':domain')

    implementation Dependencies.kotlinStdLib
    implementation Dependencies.kotlinReflectLib

    implementation Dependencies.androidxCoreLib
    implementation Dependencies.androidxAppcompatLib
    implementation Dependencies.androidxVectorDrawableLib
    implementation Dependencies.androidxLegacySupportLib
    implementation Dependencies.androidxMaterialLib

    implementation Dependencies.androidxLifecycleViewModelsLib
    implementation Dependencies.androidxLifecycleExtensionsLib

    implementation Dependencies.archNavigationFragmentLib
    implementation Dependencies.archNavigationUiLib

    implementation Dependencies.rxKotlinLib
    implementation Dependencies.rxBindingLib
    implementation Dependencies.rxRelayLib

    testImplementation Dependencies.junitLib
    testImplementation Dependencies.mockitoLib

    androidTestImplementation Dependencies.testRunnerLib
    androidTestImplementation Dependencies.espressoLib
}

task changeApkName() {
    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def date = new Date().format("yyMMdd_HHmmss")
            def fileName = "${project.ext.appName}_${variant.name}_${variant.versionName}_${date}.apk"
            output.outputFileName = fileName
        }
    }
}

gradle.taskGraph.whenReady {
    changeApkName
}